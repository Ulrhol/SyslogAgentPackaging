;NSIS Modern User Interface
;Basic Example Script
;Written by Joost Verburg

;--------------------------------
; Global macros

!define VERSION "3.6"
!define PROGRAM "Syslog Agent"
!define ARCHITECTURE "64bit"
!define NAME "${PROGRAM} v${VERSION} ${ARCHITECTURE}"

;--------------------------------
Function SplitFirstStrPart
  Exch $R0
  Exch
  Exch $R1
  Push $R2
  Push $R3
  StrCpy $R3 $R1
  StrLen $R1 $R0
  IntOp $R1 $R1 + 1
  loop:
    IntOp $R1 $R1 - 1
    StrCpy $R2 $R0 1 -$R1
    StrCmp $R1 0 exit0
    StrCmp $R2 $R3 exit1 loop
  exit0:
  StrCpy $R1 ""
  Goto exit2
  exit1:
    IntOp $R1 $R1 - 1
    StrCmp $R1 0 0 +3
     StrCpy $R2 ""
     Goto +2
    StrCpy $R2 $R0 "" -$R1
    IntOp $R1 $R1 + 1
    StrCpy $R0 $R0 -$R1
    StrCpy $R1 $R2
  exit2:
  Pop $R3
  Pop $R2
  Exch $R1 ;rest
  Exch
  Exch $R0 ;first
FunctionEnd
;--------------------------------

;--------------------------------
;Include Modern UI

  !include "MUI2.nsh"
  !include "nsDialogs.nsh"

;--------------------------------
;General

  ;Name and file
  Name "${NAME}"
  OutFile "${NAME}.exe"

  ;Default installation folder
  InstallDir "$PROGRAMFILES\${NAME}"

;--------------------------------
;Interface Settings

  !define MUI_ABORTWARNING

;--------------------------------
;Pages

  !insertmacro MUI_PAGE_LICENSE "license.txt"
  !insertmacro MUI_PAGE_COMPONENTS
  !insertmacro MUI_PAGE_DIRECTORY

  ; Here we insert our syslog target selection
  Page Custom customPage customPageLeave

  !insertmacro MUI_PAGE_INSTFILES
  

  !insertmacro MUI_UNPAGE_CONFIRM
  !insertmacro MUI_UNPAGE_INSTFILES
  
;--------------------------------
;Languages
 
  !insertmacro MUI_LANGUAGE "English"

;--------------------------------
;Installer Sections

Section "Syslog Agent" SecDummy

  SetOutPath "$INSTDIR"
  
  ;ADD YOUR OWN FILES HERE...
  File "syslogagent64\SyslogAgent\SyslogAgent.exe"
  File "syslogagent64\SyslogAgent\SyslogAgentConfig.exe"
  File "syslogagent64\SyslogAgent\license.txt"
  File "syslogagent64\SyslogAgent\gnu license.txt"

  ;Add shortcut
  createShortCut "$SMPROGRAMS\${PROGRAM}\SyslogAgentConfig.lnk" "$INSTDIR\SyslogAgentConfig.exe" "" ""
  createShortCut "$SMPROGRAMS\${PROGRAM}\Uninstall.lnk" "$INSTDIR\Uninstall.exe" "" ""
  
  ;Store installation folder
  WriteRegStr HKCU "Software\${NAME}" "" $INSTDIR
  
  ;Create uninstaller
  WriteUninstaller "$INSTDIR\Uninstall.exe"

SectionEnd

Section "Documentation" SecDoc

  SetOutPath "$INSTDIR"
  
  ;ADD YOUR OWN FILES HERE...
  File "syslogagent64\SyslogAgent\Datagram SyslogAgent manual.pdf"
  createShortCut "$SMPROGRAMS\${PROGRAM}\Datagram SyslogAgent manual.lnk" "$INSTDIR\Datagram SyslogAgent manual.pdf" "" ""
  
SectionEnd

;--------------------------------
;Descriptions

  ;Language strings
  LangString DESC_SecDummy ${LANG_ENGLISH} "SyslogAgent"
  LangString DESC_SecDoc ${LANG_ENGLISH} "Documentation"

  ;Assign language strings to sections
  !insertmacro MUI_FUNCTION_DESCRIPTION_BEGIN
    !insertmacro MUI_DESCRIPTION_TEXT ${SecDummy} $(DESC_SecDummy)
    !insertmacro MUI_DESCRIPTION_TEXT ${SecDoc} $(DESC_SecDoc)
  !insertmacro MUI_FUNCTION_DESCRIPTION_END

;--------------------------------
;Uninstaller Section

Section "Uninstall"

  ;ADD YOUR OWN FILES HERE...

  SetShellVarContext all
  Delete "$INSTDIR\SyslogAgent.exe"
  Delete "$INSTDIR\SyslogAgentConfig.exe"
  Delete "$INSTDIR\license.txt"
  Delete "$INSTDIR\gnu license.txt"
  Delete "$INSTDIR\Datagram SyslogAgent manual.pdf"
  Delete "$SMPROGRAMS\${PROGRAM}\SyslogAgentConfig.lnk" 
  Delete "$SMPROGRAMS\${PROGRAM}\SyslogAgent.lnk" 
  Delete "$SMPROGRAMS\${PROGRAM}\Datagram SyslogAgent manual.lnk"
  Delete "$SMPROGRAMS\${PROGRAM}\Uninstall.lnk"

  RMDir  "$SMPROGRAMS\${PROGRAM}"
  RMDir  "$INSTDIR"
  Delete "$INSTDIR\Uninstall.exe"
  DeleteRegKey /ifempty HKCU "Software\${NAME}"

SectionEnd

Var dialog
Var Label
Var Text

Var hwnd_listbox

/* A variable to hold the POD the user selects */
var POD
var tmppod

Function .onInit
    InitPluginsDir
    File /oname=$PLUGINSDIR\receivers.txt "receivers.txt"
FunctionEnd

Function customPage
    nsDialogs::Create 1018
    Pop $dialog

    /* Set the message dialog */

    ${NSD_CreateLabel} 0 0 100% 12u "Select syslog receiver"
    Pop $Label

    ${NSD_CreateText} 0 13u 100% 26u "Please select the host from the list to which the agent will send the log data. If you do not select one it will be possible to set manually using the SyslogAgentConfig tool from the start menu."
    Pop $Text

    /* Extract a list of currencies */
    SetOutPath "$PluginsDir"
        File "receivers.txt"

    /* Create a list box */
    ${NSD_CreateListBox} 0 42u 50% 50% ""
        Pop $hwnd_listbox
        
        /* Populate it with our list of PODs*/
        FileOpen $0 "$PluginsDir\receivers.txt" "r"
        FileRead $0 $1
        ClearErrors
        ${DoUntil} ${Errors}
            ${NSD_LB_AddString} $hwnd_listbox $1
            FileRead $0 $1
        ${Loop}
        FileClose $0
        
    /* Disabe the Next/Close button - the user -must- pick a currency */
    /* (none are selected by default in this example) */
    /* GetDlgItem $0 $HWNDPARENT 1 */
    /* EnableWindow $0 0 */

    nsDialogs::Show
FunctionEnd

Function customPageLeave
    ${NSD_LB_GetSelection} $hwnd_listbox $tmppod
    Push " " ;divider char
    Push $tmppod
    Call SplitFirstStrPart
    Pop $pod
    MessageBox MB_YESNO|MB_ICONINFORMATION "Data will be sent to: $pod" IDNO end

    ;Write the pod ip to the registry
    SetRegView 64
    WriteRegStr HKLM "SOFTWARE\Datagram\SyslogAgent" "syslog" "$pod"
    SetRegView 32
    WriteRegStr HKLM "SOFTWARE\Datagram\SyslogAgent" "syslog" "$pod"

    IfErrors 0 +2
    MessageBox MB_ICONEXCLAMATION|MB_OK "Unable to write to the registry" IDOK end
    
    end:
        ;Do nothing
FunctionEnd

;Function that calls a messagebox when installation finished correctly
Function .onInstSuccess
  MessageBox MB_OK "You have successfully installed ${PROGRAM}. Now use the SyslogAgentConfig from the start menu and click 'install' to enable the service."
FunctionEnd
 
Function un.onUninstSuccess
  MessageBox MB_OK "You have successfully uninstalled ${PROGRAM}."
FunctionEnd
